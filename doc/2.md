# Clojure for Software Engineers Part 2 - Using Spec to Validate your Data

This is part of a series [Clojure for Software Engineers](../README.md).

- [Part 1 - Core Clojure](1.md)
- **Part 2 - Using Spec to Validate your Data**

## 1. What is Spec?

By this point you are aware that Clojure is a [dynamically-typed language](https://en.wikipedia.org/wiki/Type_system).
There is no static, compile-time type checker to hold your hand. While optional type checkers for Clojure
[do exist](https://github.com/typedclojure/typedclojure), the core Clojure developers and bulk of the community 
consider other testing / checking methods to be more valuable.

Spec is library that brings runtime data validation to Clojure. Data is described in a similar way to a schema, 
which allows specification in terms of basic types without the use of a type system. We can even go further, 
describing the possible range of values, cardinality, and relationship between data. However we lose out on the 
benefits of compile-time checking, as well as more advanced concepts such as polymorphism, inheritance and generics.

| | Type Checking | Spec |
|---|---|---
|Runs|Compile Time| Run Time|
|Basic Types| X | X|
|Composition| X | X|
|Inheritance| X | |
|Generics| X | |
|Value-types| | X|
|Refactoring| X| |
|All or nothing?| X | No - partial specs supported|
|Code-intensive?| Yes | No |

We lose some important benefits by not using static type analysis. However:

1. The cost of annotating code with types can be substantial
2. Type-checking often can only catch a particular category of error - type errors
3. You must use typing everywhere for it to be valuable. Spec allows describing data only at critical points or "seams" in your program
4. You have more power to describe the nature of the values themselves
5. Spec can be used to generate sample data as part of [generative testing](https://medium.com/geckoboard-under-the-hood/how-generative-testing-changed-the-way-we-qa-geckoboard-b4a48a193449)

## 2. Writing Basic Specs

We will begin by importing the following libraries into the namespace:

```clojure
(:require [clojure.spec.alpha :as s]
          [clojure.spec.test.alpha :as st]
          [clojure.spec.gen.alpha :as gen]))
```

A spec has a name, and is used to describe and validate domain data. 

* We name the spec by using a [namespaced keyword](https://clojuredocs.org/clojure.core/keyword). Similar to a
regular keyword, this binds the spec to this particular namespace. 
* The name is tied to a predicate, a function that returns true if the data conforms to the spec.

```clojure
;; The built-in predicate `string?` is used to describe and validate
;; that a "name" is always a string
(s/def ::name string?)

;; Negative ages are not allowed 
(s/def ::age pos-int?)

;; An enumeration type, designated by a Clojure Set (which is indeed a predicate)
(s/def ::marital-status #{:single :married :divorced})

;; A custom predicate can also be used to describe data - in this case,
;; a string that is between 0 and 2000 characters long
(s/def ::biography #(and (string? %)
                         (< 0 (count %) 2000)))

;; We can use modifiers to allow a nil value
(s/def ::avatar-url (s/nilable string?))

;; We can spec sequence types also. We can define specs with different degrees of 
;; accuracy / strictness. Let's describe someone's favorite foods, in ascending
;; order of strictness:

;; 1. Can be anything
(s/def ::favorite-foods some?) 

;; 2. Must be a sequence type
(s/def ::favorite-foods vector?)

;; 3. Must be a sequence type of Strings
(s/def ::favorite-foods (s/coll-of string?))

;; 4. Must be a sequence type of non-empty Strings
(s/def ::favorite-foods (s/coll-of (s/and string? #(not (empty? %)))))

;; 5. Must be a sequence type of non-empty Strings,
;; with at least 1 element and max 5
(s/def ::favorite-foods (s/coll-of (s/and string? #(not (empty? %)))
                                   :min-count 1 :max-count 5)) 
;; 6. Must be a vector type of non-empty Strings,
;; with at least 1 element and max 5, and no duplicate elements
(s/def ::favorite-foods (s/coll-of (s/and string? #(not (empty? %)))
                                   :min-count 1 :max-count 5)) 
                                   :kind vector?
                                   :distinct true
```

We can check if data matches our spec at any time:

```clojure
(s/valid? ::favorite-foods ["Steak", "Fish and Chips"])
=> true

;; Duplicate items will fail this check
(s/valid? ::favorite-foods ["Steak", "Steak"])
=> false

;; We can ask for more detail as to why it failed:
(s/explain ::favorite-foods ["Steak", "Steak"])
["Steak" "Steak"] - failed: distinct? spec: :tetris-clj.core/favorite-foods
=> nil

;; The above error was printed to stdout as a String. Perhaps we want it returned as a piece of data
;; This way, we can build validtation logic on top of it
(s/explain-data ::favorite-foods ["Steak", "Steak"])
=>
#:clojure.spec.alpha{:problems [{:path [],
                                 :pred distinct?,
                                 :val ["Steak" "Steak"],
                                 :via [:tetris-clj.core/favorite-foods],
                                 :in []}],
                     :spec :tetris-clj.core/favorite-foods,
                     :value ["Steak" "Steak"]}
```

## 2. Spec'ing data for Tetris
We will build on top of the code we wrote for Tetris in [Part 1](1.md).

Let's write some basic specs:

```clojure

;; We will be using this for the position, and the score
(s/def ::positive-or-zero (s/and integer? #(>= % 0)))

;; A "row" in a tetris piece
(s/def ::piece-row (s/coll-of boolean? :min-count 1 :max-count 4))

;; A tetris piece - note that we can refer to existing specs (::piece-row) 
;; to build up compound specs
(s/def ::piece (s/coll-of ::piece-row :min-count 1 :max-count 4))
```

We can validate that our existing pieces passes validation:

```clojure
(def pieces
  [l-piece square-piece s-piece l2-piece z-piece line-piece])

(every?
  (fn [piece]
    (s/valid? ::piece piece))
  pieces)
=> true
```

## 3. Spec'ing functions

Writing specs for functions allows us to take validation to the next level. Once we've defined the data types for 
function arguments and return value, we can either manually or automatically perform checking whenever that function 
is called in our program. Imagine during development and testing, being confident of the shape of your data in and 
out of a function boundary. Spec could also be enabled on production code to validate user-supplied data.

To spec a function:

```clojure

(defn get-row
  "Retrieve a single row from a piece"
  [piece row-number]
  (get piece row-number))

(s/fdef get-row
       :args (s/cat :piece ::piece, :row-number ::row-number)
       :ret (s/nilable ::piece-row))
```

The `:args` section looks a little strange. The function `s/cat` allows us to define sequence *not* simply by what 
it contains, but by each item individually (positionally). Here we are defining the incoming arguments as a sequence:

1. The first item in the sequence is the piece
2. The second item in the sequence is the row number

Furthermore, Spec enforces "naming" for each of these positional data. We have named the arguments `:piece` and 
`:row-number` respectively. We will get clearer error responses by naming our arguments.

Clojure won't automatically check this function whenever it is called. We must first `instrument` the function using 
spec-test:

```clojure
;; Before, we could pass an invalid piece (only a single-dimension vector) to this functino and it would work
(get-row [true true] 1)
=> true

;; We then turn on instrumentation of function, so all specs are checked
(st/instrument `get-row)
=> [tetris-clj.core/get-row]

;; Try to pass bad data again...
(get-row [true true] 1)
Execution error - invalid arguments to tetris-clj.core/get-row at (form-init15447195903815437913.clj:1).
true - failed: coll? at: [:piece] spec: :tetris-clj.core/row
true - failed: coll? at: [:piece] spec: :tetris-clj.core/row

;; Instrumentation works even when the user doesn't call the function directly (the function
;; is instead called inside another function
(defn get-the-first-row
  [piece]
  (get-row piece 0))

(get-the-first-row [true true])
Execution error - invalid arguments to tetris-clj.core/get-row at (form-init15447195903815437913.clj:1).
true - failed: coll? at: [:piece] spec: :tetris-clj.core/row
true - failed: coll? at: [:piece] spec: :tetris-clj.core/row
```

Let's spec and instrument one of our tetris functions - a call to rotate a piece:

```clojure 
(s/fdef rotate-right :args (s/cat :piece ::piece) 
                     :ret ::piece)
                     
(st/instrument `rotate-right)
```

## 4. Generating sample data

When we describe data, we can use that description to generate sample data that matches the spec. This is a core 
part of Generative Testing.

Let's see how to generate data, and also understand where generation can fail (and how to get past it):

```clojure
;; Simple data generation
(s/def ::first-name string?)

;; We are asking "please sample some data, using the ::first-name generator"
(gen/sample (s/gen ::first-name))
=> ("" "" "C4" "mmi" "ij" "32" "yr" "5" "5Z" "43l3Z")
```

We can add further constraints to our data:

```clojure
(s/def ::first-name (s/and string? #(not (empty? %))))

(gen/sample (s/gen ::first-name))
=> ("o" "3" "Qc" "tP8" "29" "Ip" "i8" "8" "7tUG1D" "2vfs3c")
```

Let's understand what is going on here. The in-build generator is producing random strings. Each string is passed 
along the chain of predicates within `(s/and`. If it passes each predicate, it becomes valid sample data.

In the case above, empty strings will fail the second predicate and be filtered from the resulting data.

We will encounter problems if our predicates are too restrictive. Take the following for example:

```clojure
;; This spec is far to restrictive, so the standard generator won't be able to generate any valid data
(s/def ::phone-number (s/and string? #(re-matches #"\d\d\d-\d\d\d-\d\d\d\d" %)))

(s/valid? ::phone-number "123-456-7890")
=> true

(gen/sample (s/gen ::phone-number))
Error printing return value (ExceptionInfo) at clojure.test.check.generators/such-that-helper (generators.cljc:320).
Couldn't satisfy such-that predicate after 100 tries.
```

There is an infinitesimal chance that Spec will randomly generate a phone number, let alone multiple times.

In some cases, spec has additional generator functions that could be of use to us. In the case of our phone numbers 
example, we will need to take an existing generator and "munge" the output to make it fit our phone number.

In this case, we'll use a number generator to pick 10 digits, then join them into a string:

```clojure
;; Here we define a spec with a custom generator. The `fmap` function will
;; take an existing generator (in our case, one that generates a vector of digits
;; of size 10, and passes the generated result into a function (where we can convert 
;; into a string)
(s/def ::phone-number
  ;; Override the default generator
  (s/with-gen
    ;; The spec we used before
    (s/and string? #(re-matches #"\d\d\d-\d\d\d-\d\d\d\d" %))
    ;; A function containing a call to `fmap`
    #(gen/fmap
       ;; The custom function that is called to finish the generation
       (fn [digit-list]
         (let [group-1 (apply str (subvec digit-list 0 3))
               group-2 (apply str (subvec digit-list 3 6))
               group-3 (apply str (subvec digit-list 6 10))]
           (clojure.string/join "-" [group-1 group-2 group-3])))
       ;; The initial generator
       (gen/vector (s/gen #{0 1 2 3 4 5 6 7 8 9}) 10 10))))

(gen/sample (s/gen ::phone-number))
=> ("959-252-9914"
 "647-384-4262"
 "033-259-5662"
 "321-971-8473"
 "184-753-3972"
 "334-425-1572"
 "103-400-6972"
 "268-269-4058"
 "383-986-2009"
 "628-148-1467")
```

On a personal note, I find this syntax to be very tricky to use. I hope the Clojure devs improve it in an upcoming 
release.

## 5. Improving our existing Specs

Let's generate some sample data from the existing `::piece` spec:

```clojure
(s/def ::piece-row (s/coll-of boolean? :min-count 1 :max-count 4))
(s/def ::piece (s/coll-of ::piece-row :min-count 1 :max-count 4))

;; Formatted for clarity
(gen/sample (s/gen ::piece))
=> ([[false false false]
     [false false false]
     [false true true]]

    [[true true true]
     [false true]
     [false]
     [false true]]

    [[false false false false]
     [true true true false]
     [true]
     [false true]]
    ... snip ...
    )
```

It's clear that these are not valid tetris pieces. They fail in one or more of the following ways:

1. Some rows are longer than others
2. Some rows contain all `false` values
3. Some columns contain all `false` values

We need to create a better spec. not only that, we need to create a generator which will return a valid piece:

```clojure
;;
;; First some helper functions
;;
(defn empty-piece
  [size-x size-y]
  (vec (for [_ (range size-y)]
         (vec (for [_ (range size-x)]
                false)))))

(defn columns
  "Transforms a piece into column format (as opposed to row-format)"
  [piece]
  (for [x (range (width piece))]
    (column piece x)))

(defn no-empty-rows?
  "Do all rows have at least one filled cell?"
  [piece]
  (every? #(some true? %) piece))

(def no-empty-columns?
  (comp no-empty-rows? columns))

(defn all-rows-same-length?
  [piece]
  (let [width (width piece)]
    (every? #(= width (count %)) piece)))

;;
;; Our new specs
;;
(s/def ::piece-size #{1 2 3 4})
(s/def ::piece-row (s/coll-of boolean? :min-count 1 :max-count 4))
(s/def ::piece
  (s/with-gen
    ;; Spec
    (s/and (s/coll-of ::piece-row :min-count 1 :max-count 4)
           no-empty-rows?
           no-empty-columns?
           all-rows-same-length?)
    ;; Generator
    #(gen/fmap (fn [[rows cols]]
                 ;; Create a completely empty piece
                 (let [piece (empty-piece cols rows)]
                   ;; Keep looping - fill a cell at random, then check for our constraints
                   ;; Once all rows and columns are filled, exit
                   (loop [piece piece]
                     (let [row (rand-int rows)
                           col (rand-int cols)
                           piece (assoc-in piece [row col] true)]
                       (if (and (no-empty-rows? piece)
                                (no-empty-columns? piece))
                         piece
                         (recur piece))))))
               (gen/tuple (s/gen ::piece-size) (s/gen ::piece-size)))))
```

Phew! Now to generate some samples:

```clojure
;; Formatted
(gen/sample (s/gen ::piece))
=> ([[false false true]
     [true false false]
     [false true true]]

    [[true false]
     [false true]]

    [[true] [true] [true]]
    ..snip..
    )
```

## 6. Spec'ing the Game State map

Maps are one of the most crucial datastructures in Clojure for modelling data. We use it in our Tetris 
implementation to model game state: the board, the current piece, the piece position, and the player's score.

```clojure
(defn new-state
  "The state map tracks the game state: the current board and piece, the left-right position of the hovering
   piece, and current score"
  ([board piece position]
   {:board board
    :piece piece
    :position position
    :score 0}))
```

To spec a map, where each key holds a unique data element, we use the following:

```clojure
;;
;; First, we spec each individal attribute of the map
(s/def ::board-row (s/coll-of boolean? :count board-width))
(s/def ::board (s/coll-of ::board-row :count board-depth))

(s/def ::position (set (range board-width)))
(s/def ::score ::positive-or-zero)

;;
;; Now we use `keys` to specify a set of "Required" attributes in the map
;; Note the use of `req-un` - this states that our map uses un-namespaced keys
(defn valid-state?
  [state]
  (valid-drop-position? (:piece state) (:position state)))

(s/def ::state (s/and (s/keys :req-un [::board ::piece ::position ::score])
                      valid-state?))

;;
;; Let's generate a valid game states:
(take 1 (gen/sample (s/gen ::state)))
=> {:board [[false false false false false false false false]
         [false true false false false false true true]
         [true false false false false false true false]
         [true false false true true false true true]
         [false false true false false false false true]
         [false true true false true false false true]
         [true false true false true false true true]
         [true true false false true true false true]
         [false false true true false true true true]
         [false false true true true true true false]
         [true false false false true true true false]
         [false false true false false false false false]],
 :piece [[false false true] [false false true] [false true false] [true false true]],
 :position 3,
 :score 0}
```

### 7. Generative Testing real gameplay

Let's put together everything we've learned:

* Spec'ing data
* Spec'ing functions
* Instrumenting functions
* Custom generators
* Testing functions

Our goal is to randomly generate:

1) A valid game state
2) An "action" (move left, move right, rotate, drop)

... and check that the resulting game state is also a valid one (and that we don't generate any exceptions or errors 
during execution). We want to do this thousands of times with randomly generated data each time.

```clojure
(def actions #{:left :right :rotate :drop})
(s/def ::action actions)


(defn do-action
  [state action]
  (try
    (-> action
        (case
          :left (move-left state)
          :right (move-right state)
          :rotate (rotate-state state)
          :drop (drop-state state)))
    (catch Exception e
      (do (printf "Could not do action %s on %s" action state)
          (.printStackTrace e)))))


(s/fdef do-action
        :args (s/cat :state ::state, :action ::action)
        :ret ::state)

;;
;; Now, hit the `do-action` 1000 times with unique data, and check that the function returns a valid `state` map 
(st/check `do-action)
=>
({:spec #object[clojure.spec.alpha$fspec_impl$reify__2524
                0x3ec0cb81
                "clojure.spec.alpha$fspec_impl$reify__2524@3ec0cb81"],
  :clojure.spec.test.check/ret {:result true, :num-tests 1000, :seed 1647387645051},
  :sym tetris-clj.core/do-action})
```

And that's it. Through the work we've put in to spec our functions, we can be sure that our `do-action` function 
returns a valid state no matter the game state, and action taken by our user.